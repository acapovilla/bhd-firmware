/**
 * Basic SD card test
 *
 * Arduino Nano Every + MicroSD card breakout (DFR0229)
 *
 * Lib: greiman/SdFat @ 2.2.2
 *
 * Breakout working voltage = 5V
 * MISO -> D12 (CIPO SC1)
 * SCK  -> D13 (SCK)
 * SS   -> D10
 * MOSI -> D11 (COPI SC1)
 */

#include <Arduino.h>

// include the SD library:
#include "SdFat.h"

// SD card chip select
int chipSelect = 10;
#define SD_CONFIG SdSpiConfig(chipSelect, DEDICATED_SPI, SPI_HALF_SPEED)

SdFs sd;

bool failFlag = false;

void printConfig(SdSpiConfig config) {
    Serial.print(
        F("\nAssuming the SD is the only SPI device.\n"
          "Edit DISABLE_CS_PIN to disable an SPI device.\n"));

    Serial.print(F("\nAssuming the SD chip select pin is: "));
    Serial.print(int(config.csPin));
    Serial.print(F("\nEdit SD_CS_PIN to change the SD chip select pin.\n"));
}

void setup() {
    // put your setup code here, to run once:
    Serial.begin(115200);
    while (!Serial)
        ;   // wait for serial port to connect. Needed for native USB

    delay(100);
    Serial.print(F("SdFat version: "));
    Serial.println(SD_FAT_VERSION_STR);
    printConfig(SD_CONFIG);

    Serial.print("\nInitializing SD card...");

    uint32_t t = millis();
    if (!sd.cardBegin(SD_CONFIG)) {
        Serial.print(
            F("\nSD initialization failed.\n"
              "Do not reformat the card!\n"
              "Is the card correctly inserted?\n"
              "Is there a wiring/soldering problem?\n"));
        if (isSpi(SD_CONFIG)) {
            Serial.print(
                F("Is SD_CS_PIN set to the correct value?\n"
                  "Does another SPI device need to be disabled?\n"));
        }
        if (sd.sdErrorCode()) {
            Serial.print(F("SD errorCode: "));
            printSdErrorSymbol(&Serial, sd.sdErrorCode());
            Serial.print(F(" = 0x"));
            Serial.println(int(sd.sdErrorCode()));
            Serial.print(F("SD errorData = 0x"));
            Serial.println(int(sd.sdErrorData()));
        }
        while (1) delay(10);
    }
    t = millis() - t;
    Serial.print(F("init time: "));
    Serial.print(t);
    Serial.println(" ms");
}

void loop() {}
